#Область ПрограммныйИнтерфейс

// Проверяет соответствие ИНН требованиям.
//
// Параметры:
//  ИНН                - Строка - проверяемый индивидуальный номер налогоплательщика.
//  ЭтоЮридическоеЛицо - Булево - признак, является ли владелец ИНН юридическим лицом.
//  ТекстСообщения     - Строка - текст сообщения о найденных ошибках.
//
// Возвращаемое значение:
//  Булево - Истина, если соответствует.
//
Функция днкИННСоответствуетТребованиям(Знач КодИНН, ТекстСообщения = "") Экспорт

	СоответствуетТребованиям = Истина;
	ТекстСообщения = "";

	Код      = СокрЛП(КодИНН);
	ДлинаКода = СтрДлина(Код);

	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Код) Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + НСтр("ru='ИНН должен состоять только из цифр.';uk='ІПН повинен полягати тільки із цифр.'");
	КонецЕсли;

	Если ДлинаКода <> 12 Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "")
			+ НСтр("ru='ИНН юридического лица должен состоять из 12 цифр.';uk='ІПН юридичної особи повинен складатися з 12 цифр.'");
	КонецЕсли;

	Возврат СоответствуетТребованиям;

КонецФункции 

// Проверяет соответствие кода ЕГРПОУ требованиям стандартов.
//
// Параметры:
//  КодПоЕДРПОУ            - Строка - проверяемый код ЕДРПОУ.
//  ЭтоЮридическоеЛицо     - Булево - признак, является ли владелец кода ЕДРПОУ юридическим лицом.
//  ТекстСообщения         - Строка - текст сообщения о найденных ошибках.
//
// Возвращаемое значение:
//  Истина       - КодПоЕДРПОУ соответствует требованиям;
//  Ложь         - КодПоЕДРПОУ не соответствует требованиям.
//
Функция днкЕГРПОУСоответствуетТребованиям(Знач КодЕГРПОУ, ТекстСообщения = "") Экспорт
	
	СоответствуетТребованиям = Истина;
	ТекстСообщения = "";

	Код       = СокрЛП(КодЕГРПОУ);
	ДлинаКода = СтрДлина(Код);

	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Код) Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + НСтр("ru='Код по ЕГРПОУ должен состоять только из цифр.';uk='Код за ЄДРПОУ повинен полягати тільки із цифр.'");
	КонецЕсли;

	Если ДлинаКода <> 8 Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "")
		+ НСтр("ru='Код по ЕГРПОУ должен содержать 8 цифр.';uk='Код за ЄДРПОУ повинен містити 8 цифр.'");
	КонецЕсли;
	
	Если СоответствуетТребованиям Тогда 
		СоответствуетТребованиям = днкДопустимыйКодЕГРПОУ(Код, ТекстСообщения);
	КонецЕсли;
	
	Возврат СоответствуетТребованиям;
	
КонецФункции 

// Проверяет соответствие кода ГРФЛ требованиям стандартов.
//
// Параметры:
//  КодПоЕДРПОУ            - Строка - проверяемый код ЕДРПОУ.
//  ЭтоЮридическоеЛицо     - Булево - признак, является ли владелец кода ЕДРПОУ юридическим лицом.
//  ТекстСообщения         - Строка - текст сообщения о найденных ошибках.
//
// Возвращаемое значение:
//  Истина       - КодПоЕДРПОУ соответствует требованиям;
//  Ложь         - КодПоЕДРПОУ не соответствует требованиям.
//
Функция днкГРФЛСоответствуетТребованиям(Знач КодГРФЛ, ТекстСообщения = "") Экспорт
	
	СоответствуетТребованиям = Истина;
	ТекстСообщения = "";

	Код       = СокрЛП(КодГРФЛ);
	ДлинаКода = СтрДлина(Код);
	
	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Код) Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + НСтр("ru='Код по ГРФЛ должен состоять только из цифр.';uk='Код за ДРФО повинен полягати тільки із цифр.'");
	КонецЕсли;

	Если ДлинаКода <> 10 Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "")
		+ НСтр("ru='Код по ГРФЛ должен содержать 10 цифр.';uk='Код за ДРФО повинен містити 10 цифр.'");
	КонецЕсли;

	Возврат СоответствуетТребованиям;
	
КонецФункции 

// Проверяет  контрольную цифру кода ЕГРПОУ.
//
// Параметры:
//  КодПоЕДРПОУ            - Строка - проверяемый код ЕДРПОУ.
//  ТекстСообщения         - Строка - текст сообщения о найденных ошибках.
//
// Возвращаемое значение:
//  Истина       - КодПоЕДРПОУ соответствует требованиям;
//  Ложь         - КодПоЕДРПОУ не соответствует требованиям.
//
Функция днкДопустимыйКодЕГРПОУ(знач КодЕГРПОУ, ТекстСообщения = "") Экспорт
    КодСтрока = СокрЛП(КодЕГРПОУ);
 
	Коэффициенты = Новый Массив(7);
	Коэффициенты[0] = 1;
    Коэффициенты[1] = 2;
    Коэффициенты[2] = 3;
    Коэффициенты[3] = 4;
    Коэффициенты[4] = 5;
    Коэффициенты[5] = 6;
    Коэффициенты[6] = 7;
	
	КодЧисло = Число(КодСтрока);
	Если (КодЧисло > 30000000) И (КодЧисло < 60000000) Тогда 
		Коэффициенты[0] = 7;
    	Коэффициенты[1] = 1;
	    Коэффициенты[2] = 2;
    	Коэффициенты[3] = 3;
	    Коэффициенты[4] = 4;
    	Коэффициенты[5] = 5;
	    Коэффициенты[6] = 6;
	КонецЕсли;
 
	КонтрольнаяСумма = ПолучитьКонтрольнуюСумму(КодСтрока,7,Коэффициенты);
	КонтрольноеЧисло = КонтрольнаяСумма % 11;
	Если КонтрольноеЧисло > 9 Тогда
		Для А = 1 По 7 Цикл
			Коэффициенты[А-1] = Коэффициенты[А-1] + 2;
		КонецЦикла;
		КонтрольнаяСумма = ПолучитьКонтрольнуюСумму(КодСтрока,7,Коэффициенты);
		КонтрольноеЧисло = КонтрольнаяСумма % 11;
		Если КонтрольноеЧисло = 10 Тогда КонтрольноеЧисло = 0 КонецЕсли;
	КонецЕсли;
	
	Если КонтрольноеЧисло <> Число(Прав(КодСтрока, 1)) Тогда
		ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "")
		+ НСтр("ru='Недопустимый код по ЕГРПОУ.';uk='Неприпустимий код по ЄДРПОУ.'");
	    Возврат Ложь;
	КонецЕсли;
    Возврат Истина;
КонецФункции	
#КонецОбласти

#Область СлужебныеПроцедурыФункции
// Вычисляет контрольную цифру кода.
//
// Параметры:
//  КодСтрока		- Строка - проверяемый код.
//  КодДлина		- Число - длина кода без учёта контрольной цифры.
//	Коэффициенты	- Массив - коэффициенты
//
// Возвращаемое значение:
//  Число	- контрольная сумма;
//
Функция ПолучитьКонтрольнуюСумму(знач КодСтрока, знач КодДлина, Коэффициенты)
	Результат = 0;
	Для А = 1 По КодДлина Цикл
		Число = Число(Сред(КодСтрока, А, 1));
        Результат = Результат + Число * Коэффициенты[А - 1];
	КонецЦикла;
	
	Возврат Результат;
КонецФункции
#КонецОбласти
